#!/usr/bin/env perl
use strict;
use warnings;

=head1 AUTHOR

Sendu Bala <sb10@sanger.ac.uk>.

=head1 COPYRIGHT AND LICENSE

Copyright (c) 2011-2012 Genome Research Limited.

This file is part of VRPipe.

VRPipe is free software: you can redistribute it and/or modify it under the
terms of the GNU General Public License as published by the Free Software
Foundation, either version 3 of the License, or (at your option) any later
version.

This program is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE. See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License along with
this program. If not, see L<http://www.gnu.org/licenses/>.

=cut

use VRPipe::FrontEnd;

my $frontend = VRPipe::FrontEnd->new(description => "See details related to incomplete element states.",
                                     opt_spec    => [['element=i', 'Show details for this single element state only', { persistent_object => 'DataElementState' }], ['setup=s@', 'Only show details for the given PipelineSetup (by id or name); you can repeat this option', { persistent_object => 'PipelineSetup' }], ['failed|f', q[Only show elements that have have stalled in a pipeline due to repeated failures that couldn't be overcome automatically]], ['retries|r=i', q[When using --failed, this is the number of retries are needed before something is considered to have stalled], { default => 3 }], ['all|a', q[Normally only details of incomplete elements are given; this shows even ones that are done (may be very slow)]], [], [q[After the above options have been used to filter the list of all elements, and their details have been printed to screen, the following options will let you take action on those elements (after a confirmation prompt):]], ['start_from_scratch', q[For each pipeline you will be asked for a list of steps, and these will be fully reset so it is as if those steps had never been run for all the element states that went through that pipeline]], ['no_report', q[Don't print out full details of each element, just start_from_scratch as fast as possible]], ['force_with_all', q[Force --start_from_scratch to work with --all], { hidden => 1 }]]);

my $desired_ele        = $frontend->option_was_set('element');
my $failed             = $frontend->opts('failed');
my $retries            = $frontend->opts('retries');
my $all                = $frontend->opts('all');
my $start_from_scratch = $frontend->opts('start_from_scratch');
my $no_report          = $frontend->opts('no_report');
my $force_with_all     = $frontend->opts('force_with_all');

if ($all && $failed) {
    $frontend->error("--all and --failed don't make sense togother; ignoring --all");
    undef $all;
}

if ($all && !$force_with_all && $start_from_scratch) {
    $frontend->die_with_error("Sorry, --all combined with --start_from_scratch seems like a really bad idea. Use --force_with_all if you're really really sure");
}

my @element_states;
if ($desired_ele) {
    @element_states = ($frontend->opts('element'));
}
else {
    foreach my $setup ($frontend->get_pipelinesetups()) {
        if ($all) {
            @element_states = $setup->dataelementstates;
        }
        else {
            my $estates_pager = $setup->datasource->incomplete_element_states($setup);
            my @estates;
            while (my $estates = $estates_pager->next) {
                push(@estates, @$estates);
            }
            
            if ($failed) {
                my $pipeline = $setup->pipeline;
                foreach my $estate (@estates) {
                    my @sms = VRPipe::StepMember->search({ step_number => ($estate->completed_steps + 1), pipeline => $pipeline->id });
                    
                    my $step_member;
                    if (@sms == 1) {
                        $step_member = $sms[0];
                    }
                    else {
                        $frontend->error("element state ", $estate->id, " didn't have exactly 1 corresponding step member; skipping");
                        next;
                    }
                    
                    my $sstate = VRPipe::StepState->get(stepmember => $step_member, dataelement => $estate->dataelement, pipelinesetup => $setup);
                    if ($sstate->complete) {
                        $frontend->error("weird, I meant to get a failed stepstate for estate ", $estate->id, " at step ", ($estate->completed_steps + 1), " but it was complete! Skipping");
                        next;
                    }
                    
                    my ($found) = VRPipe::Submission->search({ retries => $retries, '_failed' => 1, stepstate => $sstate->id });
                    if ($found) {
                        #*** can we combine all the above into one big search search query with multiple joins?...
                        push(@element_states, $estate);
                    }
                }
            }
            else {
                push(@element_states, @estates);
            }
        }
    }
}

my $count = 0;
my %summary;
my %for_scratching;
foreach my $estate (@element_states) {
    my $setup           = $estate->pipelinesetup;
    my $pipeline        = $setup->pipeline;
    my @stepms          = $pipeline->step_members;
    my $num_steps       = @stepms;
    my $pipe_name       = $pipeline->name . " ($num_steps steps)";
    my $completed_steps = $estate->completed_steps;
    my $last_step_name  = 'no_steps_complete';
    foreach my $stepm (@stepms) {
        next unless $stepm->step_number == $completed_steps;
        my $step = $stepm->step;
        $last_step_name = $stepm->step->name . " (step $completed_steps)";
    }
    
    $count++;
    $summary{$pipe_name}->{$last_step_name}++;
    push(@{ $for_scratching{ $pipeline->id } }, $estate);
    
    next if $no_report;
    
    my $step_desc;
    if ($completed_steps == $num_steps) {
        $step_desc = ': complete!';
    }
    else {
        my $current_step_num = $completed_steps + 1;
        my ($step_member) = grep { $_->step_number == $current_step_num } @stepms;
        my $sstate = VRPipe::StepState->get(stepmember => $step_member, dataelement => $estate->dataelement, pipelinesetup => $setup);
        
        my @subs = VRPipe::Submission->search({ stepstate => $sstate->id }, { prefetch => 'job' });
        my ($num_failed, $num_perm_failed, $num_done, $num_scheduled, $num_running) = (0, 0, 0, 0, 0);
        foreach my $sub (@subs) {
            if ($sub->failed) {
                if ($sub->retries >= $retries) {
                    $num_perm_failed++;
                }
                else {
                    $num_failed++;
                }
            }
            elsif ($sub->done) {
                $num_done++;
            }
            else {
                if ($sub->job->running) {
                    $num_running++;
                }
                else {
                    $num_scheduled++;
                }
            }
        }
        
        my $current_step_name = $step_member->step->name;
        my $status = $num_perm_failed ? 'STALLED' : 'working';
        my @subs_desc;
        if ($num_perm_failed) {
            push(@subs_desc, "$num_perm_failed PERMANENTLY failed");
        }
        if ($num_failed) {
            push(@subs_desc, "$num_failed temporarily failed");
        }
        if ($num_done) {
            push(@subs_desc, "$num_done finished ok");
        }
        if ($num_running) {
            push(@subs_desc, "$num_running currently running");
        }
        if ($num_scheduled) {
            push(@subs_desc, "$num_scheduled scheduled to run");
        }
        my $subs_desc;
        if (@subs_desc) {
            $subs_desc = '(submission breakdown = ' . join(', ', @subs_desc) . ')';
        }
        else {
            $subs_desc = '(no submissons created for step state ' . $sstate->id . ' yet)';
        }
        
        $step_desc = ": currently $status on $current_step_name $subs_desc";
    }
    
    $frontend->output("ElementState ", $estate->id, " for pipeline $pipe_name in setup ", $setup->id, " completed step $last_step_name$step_desc");
}

unless ($desired_ele) {
    unless ($count == 0 || $no_report) {
        $frontend->output("\n");
    }
    $frontend->output("$count element states passed your filter");
    if ($count) {
        $frontend->output("Here's the break-down by the name of the step the element last completed successfully:");
        while (my ($pipe_name, $step_details) = each %summary) {
            $frontend->display_hash("\t$pipe_name", $step_details);
        }
    }
}

if ($count > 0 && $start_from_scratch) {
    while (my ($pipeline_id, $estates) = each %for_scratching) {
        my $count    = @$estates;
        my $pipeline = VRPipe::Pipeline->get(id => $pipeline_id);
        my $answer   = $frontend->ask_question(question => "Are you sure you want to start $count data element states over from scratch for steps (that you'll choose next) in pipeline?", possibles => [qw(y n)], default => 'n');
        if ($answer eq 'y') {
            # give the user a step list to choose from, and provide them a
            # reasonable default
            $frontend->output("Provide a space-separated list of step numbers you'd like to start over from scratch (you should avoid steps that produce output shared by multiple elements/pipelines):");
            foreach my $stepm ($pipeline->step_members) {
                $frontend->output('  ', $stepm->step_number, ': ', $stepm->step->name);
            }
            my $furthest_estate;
            my $furthest = 0;
            foreach my $estate (@$estates) {
                my $completed = $estate->completed_steps;
                if ($completed >= $furthest) {
                    $furthest        = $completed;
                    $furthest_estate = $estate;
                }
            }
            my @default_steps = $furthest_estate->our_step_numbers;
            
            my $max = $pipeline->_num_steps;
            $answer = $frontend->ask_question(question => "Step numbers", default => join(' ', @default_steps), required => 1, possibles => [1 .. $max], allow_multiple => ' ');
            my @desired_steps = sort { $a <=> $b } grep { $_ =~ /^\d+$/ && $_ >= 1 && $_ <= $max } split(/\s+/, $answer);
            $frontend->output("Will redo steps ", join(', ', @desired_steps));
            
            foreach my $estate (@$estates) {
                $estate->start_from_scratch(\@desired_steps);
            }
            $frontend->output("Done");
        }
        else {
            $frontend->output("You said no, so skipping");
        }
    }
}

exit;
